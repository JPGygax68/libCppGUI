# Sub-library for libCppGUI, packaging pre-rasterized fonts

project(cppgui_baked_fonts)


#------------------------------------------------------------------------------
# Prerequisite: font rasterizer command-line tool
#

add_subdirectory(rasterizer)


#------------------------------------------------------------------------------
# Custom rules to generate constant data files
#

set(font_file LiberationSans-Regular)

string(TOLOWER ${font_file} font_name_as_id)
string(REPLACE "-" "_" font_name_as_id ${font_name_as_id})

# TODO: support for sizes by default, make them configurable
# TODO: use a CMake custom function (to be provided by GPCFontRasterizer) as soon as available

file(WRITE ${GENERATED_HEADERS_DIR}/liberationsans_font.h "// liberationsans_font.h  - generated by CMake, DO NOT MODIFY\n")

file(APPEND ${GENERATED_HEADERS_DIR}/liberationsans_font.h
    "template<std::size_t Size> struct ${font_name_as_id} { static constexpr auto get() -> std::pair<const uint8_t *, std::size_t> { return {nullptr, 0}; } };\n\n")

foreach(size IN ITEMS 16 24)

    add_custom_command(
        OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/generated/rastfont/${font_file}-${size}.rastfont
        DEPENDS ${CMAKE_CURRENT_SOURCE_DIR}/data/fonts/${font_file}.ttf
        COMMAND gpcfontrasterizer ARGS
            input=${CMAKE_CURRENT_SOURCE_DIR}/data/fonts/${font_file}.ttf
            size=${size}
            output="${CMAKE_CURRENT_BINARY_DIR}/generated/rastfont/${font_file}-${size}.rastfont"
            range=all
    )

    add_custom_command(
        OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/generated/headers/${font_file}-${size}.h
        OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/generated/sources/${font_file}-${size}.c
        DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/generated/rastfont/${font_file}-${size}.rastfont
        COMMAND gpcbin2c ARGS
            -i "${CMAKE_CURRENT_BINARY_DIR}/generated/rastfont/${font_file}-${size}.rastfont"
            -c ${font_name_as_id}_${size}
            -h "${CMAKE_CURRENT_BINARY_DIR}/generated/headers/${font_file}-${size}.h"
            -s "${CMAKE_CURRENT_BINARY_DIR}/generated/sources/${font_file}-${size}.c"
    )

    file(APPEND ${GENERATED_HEADERS_DIR}/liberationsans_font.h "extern \"C\" {\n")
    file(APPEND ${GENERATED_HEADERS_DIR}/liberationsans_font.h "#include \"${font_file}-${size}.h\"\n")
    file(APPEND ${GENERATED_HEADERS_DIR}/liberationsans_font.h "}\n")

    list(APPEND FONT_HEADERS ${CMAKE_CURRENT_BINARY_DIR}/generated/headers/${font_file}-${size}.h)
    list(APPEND FONT_SOURCES ${CMAKE_CURRENT_BINARY_DIR}/generated/sources/${font_file}-${size}.c)

    if (0)
    file(APPEND ${GENERATED_HEADERS_DIR}/liberationsans_font.h "
template<> struct ${font_name_as_id}<${size}> {
    static constexpr auto get() -> std::pair<const uint8_t *, std::size_t> {
        return { ${font_name_as_id}_${size}, sizeof(${font_name_as_id}_${size}) };
    }
};

")
    endif(0)

endforeach(size)

# TODO: support a variety of sizes

set(font_file MaterialIcons-Regular)

# TODO: change the name
file(WRITE ${GENERATED_HEADERS_DIR}/all_icon_fonts.h "// all_icon_fonts.h  - generated by CMake, DO NOT MODIFY\n")

file(APPEND ${GENERATED_HEADERS_DIR}/all_icon_fonts.h
    "template<std::size_t Size> struct materialicons_regular{ static auto get() -> std::pair<const uint8_t *, std::size_t> { return {nullptr, 0}; } };\n\n")

foreach(size IN ITEMS 12 13 16 20 24 32 36)

    add_custom_command(
        OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/generated/rastfont/${font_file}-${size}.rastfont
        DEPENDS ${CMAKE_CURRENT_SOURCE_DIR}/data/fonts/${font_file}.ttf
        COMMAND gpcfontrasterizer ARGS
            input=${CMAKE_CURRENT_SOURCE_DIR}/data/fonts/${font_file}.ttf
            size=${size}
            output="${CMAKE_CURRENT_BINARY_DIR}/generated/rastfont/${font_file}-${size}.rastfont"
            range=$E876 range=$E14C range=$E154 range=$E15E range=$E834 range=$E835 range=$E836 range=$E837 range=$E5C5 range=$E5C7
    )

    add_custom_command(
        OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/generated/headers/${font_file}-${size}.h
        DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/generated/rastfont/${font_file}-${size}.rastfont
        COMMAND gpcbin2c ARGS
            -i "${CMAKE_CURRENT_BINARY_DIR}/generated/rastfont/${font_file}-${size}.rastfont"
            -o "${CMAKE_CURRENT_BINARY_DIR}/generated/headers/${font_file}-${size}.h"
    )

    list(APPEND FONT_HEADERS ${CMAKE_CURRENT_BINARY_DIR}/generated/headers/${font_file}-${size}.h)

    string(TOLOWER "${font_file}" identifier)
    string(REPLACE "-" "_" identifier ${identifier})
    set(identifier "${identifier}_${size}")

    if (0)
    file(APPEND ${GENERATED_HEADERS_DIR}/all_icon_fonts.h "static const uint8_t ${identifier}[] = {\n")
    file(APPEND ${GENERATED_HEADERS_DIR}/all_icon_fonts.h "    #include \"${font_file}-${size}.h\"\n")
    file(APPEND ${GENERATED_HEADERS_DIR}/all_icon_fonts.h "};\n\n")

    file(APPEND ${GENERATED_HEADERS_DIR}/all_icon_fonts.h "
template<> struct materialicons_regular<${size}>{ static auto get() -> std::pair<const uint8_t *, std::size_t> {
        return { materialicons_regular_${size}, sizeof(materialicons_regular_${size}) };
    }
};
")
    endif(0)

endforeach(size)


#------------------------------------------------------------------------------
# Main target: static sub-library
#

add_library(${PROJECT_NAME}
STATIC
    ${FONT_HEADERS}
    ${FONT_SOURCES}
    include/cppgui/baked_fonts.hpp      src/baked_fonts.cpp
    ${PROJECT_BINARY_DIR}/cppgui_baked_fonts_config.hpp
)

target_include_directories(${PROJECT_NAME} PRIVATE ${PROJECT_SOURCE_DIR}/include/cppgui)
target_include_directories(${PROJECT_NAME} PUBLIC  ${PROJECT_SOURCE_DIR}/include)

# Generate configuration file and make it available to target and consumers
configure_file("${PROJECT_SOURCE_DIR}/cppgui_baked_fonts_config.in.hpp"
               "${PROJECT_BINARY_DIR}/cppgui_baked_fonts_config.hpp" )
target_include_directories(${PROJECT_NAME} PUBLIC ${PROJECT_BINARY_DIR})


